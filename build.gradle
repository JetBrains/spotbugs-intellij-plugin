#!groovy
/*
 * Copyright 2020 SpotBugs plugin contributors
 *
 * This file is part of IntelliJ SpotBugs plugin.
 *
 * IntelliJ SpotBugs plugin is free software: you can redistribute it 
 * and/or modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation, either version 3 of 
 * the License, or (at your option) any later version.
 *
 * IntelliJ SpotBugs plugin is distributed in the hope that it will
 * be useful, but WITHOUT ANY WARRANTY; without even the implied 
 * warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with IntelliJ SpotBugs plugin.
 * If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id "org.jetbrains.intellij" version "1.16.1"
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'maven-publish'
apply plugin: 'java'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version = ijVersion

    pluginName = 'spotbugs-idea'
    updateSinceUntilBuild = false
    type = 'IC'

    plugins = ['git4idea', 'java']
}

jacoco {
    toolVersion = "0.8.5"
}

runIde {
    systemProperties(["jdk.util.zip.ensureTrailingSlash":false])
}

configurations {
    thirdPartyPlugins
    fbContribJava6
}

def remoteRobotVersion = "0.9.35"
def fixturesVersion = "1.1.18"

dependencies {
    implementation ('com.github.spotbugs:spotbugs:4.8.6') {
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    implementation 'net.sf.saxon:Saxon-HE:12.3'
    implementation 'org.jsoup:jsoup:1.16.1'
    implementation 'info.clearthought:table-layout:4.3.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    thirdPartyPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.6.0'
    thirdPartyPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    fbContribJava6 'com.mebigfatguy.fb-contrib:fb-contrib:6.2.1'
    testImplementation("com.intellij.remoterobot:remote-robot:$remoteRobotVersion")
    testImplementation("com.intellij.remoterobot:remote-fixtures:$fixturesVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

tasks.register('copyGradleProperties', Copy) {
    description 'Copy gradle.properties to project resources.'
    from 'gradle.properties'
    into 'build/resources/main/org/jetbrains/plugins/spotbugs/common'
    rename { _ -> 'version.properties' }
}

tasks.register('downloadThirdPartyPlugins', Copy) {
    description 'Downloads third-party plugins Find Security Bugs and FB-Contrib.'
    from configurations.thirdPartyPlugins
    from configurations.fbContribJava6
    into 'build/resources/main/org/jetbrains/plugins/spotbugs/plugins'
}

tasks.register('copyThirdPartyPlugins', Copy) {
    dependsOn(downloadThirdPartyPlugins, prepareSandbox)
    description 'Copy plugins into sandbox.'
    from 'build/resources/main/org/jetbrains/plugins/spotbugs/plugins'
    into 'build/idea-sandbox/plugins/spotbugs-idea/customPlugins'
}

tasks.register('deleteThirdPartyPlugins', Delete) {
    delete 'build/resources/main/org/jetbrains/plugins/spotbugs/plugins'
}

tasks.compileJava.dependsOn(downloadThirdPartyPlugins, copyGradleProperties)
tasks.buildPlugin.dependsOn(copyThirdPartyPlugins)
tasks.jacocoTestReport.dependsOn(deleteThirdPartyPlugins)
tasks.jacocoTestCoverageVerification.dependsOn(deleteThirdPartyPlugins)

downloadRobotServerPlugin.version = remoteRobotVersion

runIdeForUiTests {
    systemProperty "robot-server.port", "8082"
}

tasks.test.dependsOn(runIdeForUiTests)

test {
    def ideaHomePath = System.getenv().get('IDEA_HOME_PATH') ?: project.findProperty('idea.home.path')
    if (ideaHomePath == null) {
        throw new RuntimeException("Please set the 'idea.home.path' property at gradle.properties")
    }
    systemProperty "idea.home.path", ideaHomePath
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

buildSearchableOptions.enabled = false

wrapper {
    gradleVersion = '6.3'
}
